# -*- mode:shell-script -*-
path=(/usr/local/sbin /usr/local/bin /usr/sbin /usr/bin /sbin /bin)

# completion
fpath=(~/.zsh/functions ~/.zsh/zsh-completions/src ~/.zsh/completions $fpath)
autoload -U compinit
compinit

# default umask
umask 022

# for move by word
autoload -Uz select-word-style
select-word-style bash
WORDCHARS="${WORDCHARS}:"

# insert last word
autoload -Uz smart-insert-last-word
zle -N insert-last-word smart-insert-last-word

# History
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt share_history

# key-type is Emacs
bindkey -e

# Like bash C-u behavior
bindkey '^U' backward-kill-line

# Mode/Delete like emacs symbol one
bindkey '^[^B' vi-backward-blank-word
bindkey '^[^F' vi-forward-blank-word
bindkey '^[^K' delete-word

# removed keys(for miss typing)
# I want to bind good function for these keys!!
bindkey -r '^J'

# for incremental search
bindkey '^R' history-incremental-pattern-search-backward
bindkey '^S' history-incremental-pattern-search-forward

# Disable completion command
# following command completion is too long.
compdef -d java

# not exit Ctrl-D
setopt ignoreeof

# for sighup
setopt NOHUP
setopt nocheckjobs

# stty
stty stop undef

# completion like emacs
zstyle ':completion:*:default' menu select=1

# completion use cache
zstyle ':completion:*' use-cache true

# completion after '='(like --prefix=/usr)
setopt magic_equal_subst

# with slash
setopt noautoremoveslash

# glob expand
setopt null_glob no_nomatch

# print if line is one line
unsetopt promptcr

# Not bell
setopt NO_beep
setopt nolistbeep

# String behind '#' is comment.
setopt interactive_comments

# key binding of command history
autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^P" history-beginning-search-backward-end
bindkey "^N" history-beginning-search-forward-end

# process completion
zstyle ':completion:*:processes' command "ps -u $USER"

# show processing time for long run process
REPORTTIME=10

# setting LANG, this should be UTF-8
export LANG=ja_JP.UTF-8
export TERM=xterm-256color

# setup ls coloring
local LIST_COLOR='di=34;1' 'ln=35' 'so=32' 'ex=32;1' 'bd=46;34' 'cd=43;34'
zstyle ':completion:*' list-colors $LIST_COLOR
if [ "$OSTYPE" == "linux-gnu" ]; then
    alias ls='ls --color -F'
    if [ ! -e ~/.dir_colors ]; then
        dircolors -p > ~/.dir_colors
    fi
    eval `dircolors ~/.dir_colors -b`
else
    if which gls > /dev/null 2>&1; then
        alias ls='gls --color -F'
    fi
fi

# dictionary
export EIJIRO_DIR=${HOME}/local/dict

# alias
alias rdesktop='rdesktop -x l -a 16 -g 90%'
alias grep='grep --color=always'
alias e='emacsclient'
alias s='git st '
alias d='git diff '
alias W='weblio'
alias cdep='cde >/dev/null && cdp && pwd'

# dstat
alias dstat-full='dstat -Tclmdrn'
alias dstat-mem='dstat -Tclm'
alias dstat-cpu='dstat -Tclr'
alias dstat-net='dstat -Tclnd'
alias dstat-disk='dstat -Tcldr'

# send to clipboard for each platform
if which pbcopy >/dev/null 2>&1 ; then
    alias -g C='| pbcopy'
elif which xsel >/dev/null 2>&1 ; then
    # Linux
    alias -g C='| xsel --input --clipboard'
fi

# PROMPT
autoload colors
colors
PROMPT="%B%{${fg[yellow]}%}%n@%m:%{${reset_color}%}%b%# "

# EDITOR
export EDITOR=vim
export GIT_EDITOR=emacsclient

# PAGER setting
export PAGER='less'
export LESSCHARSET=utf-8

# info
export INFOPATH=/usr/local/share/info:/usr/share/info

# Perl
export PERLDOC_PAGER='less -R' # for new Pod::Perldoc
if [ -d ~/.plenv/bin ]; then
    path=($HOME/.plenv/bin $path)
    eval "$(plenv init -)"
fi

# Ruby
if [ -d ~/.rbenv/bin ]; then
    path=($HOME/.rbenv/bin $path)
    eval "$(rbenv init - zsh)"
fi

# Javascript
if [ -e ~/.nvm/nvm.sh ]; then
    source ~/.nvm/nvm.sh
    nvm use default > /dev/null
fi

# Python
VIRTUALENV_ACTIVE=~/.virtualenv/bin/activate
if [ -e ${VIRTUALENV_ACTIVE} ]; then
    export VIRTUAL_ENV_DISABLE_PROMPT=1
    source ${VIRTUALENV_ACTIVE}
fi
export PYTHONSTARTUP=~/.pythonsetup

function ipython() {
    env LESS="-r" ipython $@
}

## Cask
path=(~/.cask/bin(N-/) $path)

# haskell setting
path=(~/.cabal/bin(N-/) $path)

function godoc() {
    if [ "$1" == "" ]; then
        godocp
    else
        command godoc $@ | less
    fi
}

# Go Language
if [[ -e /opt/go/bin/go ]]; then
    path=(/opt/go/bin $path)
    export GOROOT=/opt/go
fi

export GOPATH=$HOME/go
path=($GOPATH/bin $path)

which direnv >/dev/null 2>&1 && eval "$(direnv hook zsh)"

# Rust
path=(~/.cargo/bin(N-/) $path)

# Heroku
path=(/usr/local/heroku/bin $path)

# Google Cloud Platform
if [[ -d ~/local/google-cloud-sdk ]]; then
    source ~/local/google-cloud-sdk/path.zsh.inc
    source ~/local/google-cloud-sdk/completion.zsh.inc
fi

## show git branch at right prompt
autoload -Uz vcs_info
zstyle ':vcs_info:*' formats '(%b)'
zstyle ':vcs_info:*' actionformats '[%s]-(%b|%a)'
precmd () {
    psvar=()
    LANG=en_US.UTF-8 vcs_info
    [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"

    if git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
        stashes=$(git stash list 2>/dev/null | wc -l)
        if [ ${stashes} -eq 0 ]; then
            psvar[2]=""
        else
            psvar[2]="@${stashes// /}"
        fi
    fi
}
_DIRPART=%B%{$fg[cyan]%}'[%(5~,%-2~/.../%2~,%~)]'%{$reset_color%}%b
RPROMPT=%B"%1(v|%F{magenta}%1v%f%F{yellow}%2v%f|)"%b${_DIRPART}

autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
zstyle ':chpwd:*' recent-dirs-max 500
zstyle ':chpwd:*' recent-dirs-default yes
zstyle ':completion:*' recent-dirs-insert both
zstyle ':filter-select' case-insensitive yes

# zaw : anything
if [ -e ~/.zsh/zaw/zaw.zsh ]; then
    source ~/.zsh/zaw/zaw.zsh
    ZAW_EDITOR='emacsclient -n'
    bindkey '^x^x' zaw-cdr
    bindkey '^xd' zaw-git-directories
    bindkey '^xb' zaw-git-recent-branches
    bindkey '^x^f' zaw-git-files
fi

# for git completion(too slowly in big repository)
__git_files () {
    _wanted files expl 'local files' _files
}

# for tmux complete
_tmux_pane_words() {
    local expl
    local -a w
    if [[ -z "$TMUX_PANE" ]]; then
        _message "not running inside tmux!"
        return 1
    fi
    w=( ${(u)=$(tmux capture-pane \; show-buffer \; delete-buffer)} )
    _wanted values expl 'words from current tmux pane' compadd -a w
}

zle -C tmux-pane-words-prefix   complete-word _generic
zle -C tmux-pane-words-anywhere complete-word _generic
bindkey '^[/' tmux-pane-words-prefix
bindkey '^[?' tmux-pane-words-anywhere
zstyle ':completion:tmux-pane-words-(prefix|anywhere):*' completer _tmux_pane_words
zstyle ':completion:tmux-pane-words-(prefix|anywhere):*' ignore-line current
zstyle ':completion:tmux-pane-words-anywhere:*' matcher-list 'b:=*'

## github browse
bindkey -s "^Xww" " hub browse"
bindkey -s "^Xwi" " hub browse -- issues"
bindkey -s "^Xwp" " hub browse -- pulls"
bindkey -s "^Xwu" " hub browse -u"

## My commands
zle -N peco-history _peco-history
zle -N delete_previous_word _delete_previous_word
bindkey '^X^R' peco-history
bindkey '^[^U' delete_previous_word

autoload -Uz _git_browse
autoload -Uz us_keyboard
autoload -Uz cde
autoload -Uz cdp
autoload -Uz cdg
autoload -Uz dired
autoload -Uz _peco-history
autoload -Uz _delete_previous_word

## Last
# setting global alias for avoiding conflict variable name
alias -g L='|less -R'
alias -g G='|grep'
alias -g P='|peco'
alias -g J='|jq'

path=($HOME/bin $path)
